// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Put your code here:

    // registerLoad = reset OR load OR inc bit
    Or(a=reset, b=load, out=ltemp);
    Or(a=ltemp, b=inc,  out=registerLoad);
  
    // Here I implement the if-else chain in reverse

    Inc16(in=outcopy, out=incremented);

    // The a input for the first MUX should actually be 'dont-care', but false is the next best thing
    Mux16(a=false, b=incremented, sel=inc, out=outinc);
    Mux16(a=outinc, b=in, sel=load, out=outload);
    Mux16(a=outload, b=false, sel=reset, out=registerInput);  
    Register(in=registerInput, load=registerLoad, out=out, out=outcopy);
}
